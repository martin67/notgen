plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'io.freefair.lombok' version '6.6.1'
    id 'com.gorylenko.gradle-git-properties' version '2.4.1'
    id 'org.liquibase.gradle' version '2.1.1'
    id "org.sonarqube" version "3.5.0.2730"
    id "jacoco"
}

sonarqube {
    properties {
        property "sonar.projectKey", "martin67_notgen2"
        property "sonar.organization", "martin-hagelin"
        property "sonar.host.url", "https://sonarcloud.io"
        property 'sonar.branch.name', gitBranch()
    }
}

static String gitBranch() {
    String branch = ""
    Process proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    return branch
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform() {
        excludeTags 'manual'
    }
    systemProperty "file.encoding", "UTF-8"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

springBoot {
    buildInfo()
}

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

configurations {
    liquibaseRuntime.extendsFrom runtimeClasspath
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
//    implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time'
    implementation 'org.liquibase:liquibase-core:4.17.2'
    implementation 'mysql:mysql-connector-java:8.0.30'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.0.6'
    implementation 'com.microsoft.sqlserver:mssql-jdbc:11.2.1.jre17'
    implementation 'com.h2database:h2:2.1.214'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation platform('software.amazon.awssdk:bom:2.19.27')
    implementation 'software.amazon.awssdk:s3'
    implementation 'com.azure.spring:spring-cloud-azure-dependencies:5.0.0'
    implementation 'com.azure.spring:spring-cloud-azure-starter-storage:5.0.0'
    implementation 'com.azure.spring:spring-cloud-azure-starter-keyvault:5.0.0'
    implementation 'commons-cli:commons-cli:1.5.0'
    implementation 'org.apache.commons:commons-csv:1.9.0'
    implementation 'org.apache.commons:commons-imaging:1.0-alpha3'
    implementation 'org.apache.pdfbox:pdfbox:2.0.27'
    implementation 'org.apache.pdfbox:jbig2-imageio:3.0.4'
    implementation 'net.imagej:ij:1.53e'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.8.0'
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.3.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    liquibaseRuntime 'info.picocli:picocli:4.7.1'
    liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate6:4.19.0'
    liquibaseRuntime sourceSets.main.output

    liquibase {
        activities {
            main {
                changeLogFile 'src/main/resources/db/changelog/db.changelog-master.yaml'
                //changeLogFile 'db/changelog/db.changelog-master.yaml'
                //changeLogFile 'db.changelog-master.yaml'
                //classpath 'src/main/resources'
                //url "jdbc:h2:file:./todb"
                defaultSchemaName "dbo"
                referenceUrl "hibernate:spring:se.terrassorkestern.notgen.model?dialect=org.hibernate.dialect.SQLServerDialect&hibernate.physical_naming_strategy=org.hibernate.boot.model.naming.CamelCaseToUnderscoresNamingStrategy&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy"
                url System.getenv("SPRING_DATASOURCE_URL")
                username System.getenv("SPRING_DATASOURCE_USERNAME")
                password System.getenv("SPRING_DATASOURCE_PASSWORD")
            }
        }
    }
}

